-- Identity
id : (A : U) -> A -> A
id := \A. \a. a

#check id (1 x 1) (*, *)

-- Projections
fst : (A : U) -> (B : A -> U) -> (a : A) x B a -> A
fst := \A. \B. \p. ind[(a : A) x B a](A, a. b. a, p)

#check fst

snd : (A : U) -> (B : A -> U) -> (p : (a : A) x B a) -> B (fst A B p)
snd := \A. \B. \p. ind[(a : A) x B a](p. B (fst A B p), a. b. b, p)

#check snd

-- Type theoretic axiom of choice
ac : (A : U) -> (B : U) -> (R : A -> B -> U) -> ((a : A) -> (b : B) x (R a b)) -> ((f : A -> B) x ((a : A) -> (R a (f a))))
ac := \A. \B. \R. \g. (\(a : A). fst B (\(b : B). R a b) (g a), \(a : A). snd B (\(b : B). R a b) (g a))

#check ac

-- Boolean
Bool := 1 + 1

#check Bool

true : Bool
true := inl(*)

false : Bool
false := inr(*)

#check true
#check false

-- Conditional
ifThenElse : (C : U) -> Bool -> C -> C -> C
ifThenElse := \C. \b. \c1. \c2. ind[Bool](C, a. c1, b. c2, b)

#check ifThenElse

not : Bool -> Bool
not := \b. ifThenElse Bool b false true

#check not false

-- Equality
starEq : * = *
starEq := refl[*]

#check starEq
